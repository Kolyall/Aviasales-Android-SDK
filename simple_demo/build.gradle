apply plugin: 'com.android.application'

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'http://android.aviasales.ru/repositories/' }
    maven {
        url 'http://aviasalesandroid.fvds.ru:8080/nexus/content/repositories/as_android_beta/'
        credentials {
            username mavenUser
            password mavenPassword
        }
    }
}

def gitBranch = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "ru.aviasales.alllife"
        minSdkVersion 14
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0.1"
    }
    buildTypes {
        debug {
            ext.betaDistributionNotifications = true
            ext.betaDistributionGroupAliases = "as-android-dev"
            versionNameSuffix "debug"
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }
}

android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        def suffix = ""
        if (gitBranch != "master" && gitBranch != "HEAD") {
            suffix += "-${gitBranch}"
        }
        if (variant.buildType.versionNameSuffix) {
            suffix += "-${variant.buildType.versionNameSuffix}"
        }
        def newName = "${android.defaultConfig.applicationId}-v${android.defaultConfig.versionName}-vc${android.defaultConfig.versionCode}${suffix}"

        //noinspection GroovyAssignabilityCheck
        output.outputFile = new File(output.outputFile.parentFile, newName + ".apk")
        print "APK Name: ${output.outputFile}"
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':aviasalestemplate')

    compile "com.google.android.gms:play-services-ads:$rootProject.ext.gmsVersion"
}
